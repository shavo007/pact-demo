buildscript {
	ext {
		springBootVersion = '1.4.0.RC1'
	}
	repositories {
		mavenCentral()
		maven { url "https://repo.spring.io/snapshot" }
		maven { url "https://repo.spring.io/milestone" }
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath 'au.com.dius:pact-jvm-provider-gradle_2.11:3.2.7'
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'spring-boot'
apply plugin: 'au.com.dius.pact'
apply plugin: 'idea'


jar {
	baseName = 'pact-demo'
	version = '0.0.1-SNAPSHOT'
}
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	mavenCentral()
	maven { url "https://repo.spring.io/snapshot" }
	maven { url "https://repo.spring.io/milestone" }
}


dependencies {
	compile('org.springframework.boot:spring-boot-starter-actuator')
	compile('org.projectlombok:lombok')
	compile('org.springframework.boot:spring-boot-starter-web')
	testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('au.com.dius:pact-jvm-consumer-junit_2.11:3.2.10')
    testCompile('io.rest-assured:rest-assured:3.0.0')
}


eclipse {
	classpath {
		 containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
		 containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
	}
}

test {

    systemProperties['pact.rootDir'] = "$buildDir/pacts"

}

pact {

    publish {
        pactBrokerUrl = 'http://localhost:5000'
        version = '1.0'
    }

    serviceProviders {
        provider1 {
            // Only load the pacts from the broker if the start tasks from the command line include pactVerify
            if ('pactVerify' in gradle.startParameter.taskNames) {
                hasPactsFromPactBroker('http://localhost:5000/') { consumer ->
                    stateChange = { providerState -> /* state change code here */ true }
                }
            }
        }
    }

    reports {
        defaultReports() // adds the standard console output

        markdown // report in markdown format
        json // report in json format
    }

}


